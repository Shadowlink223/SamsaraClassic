// I've been here before...

ACTOR BeggarB : Beggar replaces Beggar
{
    DamageFactor "CorvusCrossbow", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255

    +NOTDMATCH
    +NOICEDEATH

    States
    {
      Death.DukeDamage:
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        goto "Super::Death"

      Death.DukeExplosive:
      Death.DukeSpectral:
        TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
        goto Death.DukeDamage

      DukeGibStart:
        TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_SpawnDebris("DukeGibs1")
        TNT1 A 0 A_SpawnDebris("DukeGibs2")
        TNT1 A 0 A_SpawnDebris("DukeGibs3")
        TNT1 A 0 A_SpawnDebris("DukeGibs4")
        TNT1 A 0 A_SpawnDebris("DukeGibs5")
        TNT1 A 0 A_SpawnDebris("DukeGibs6")
        TNT1 A 0 A_PlaySound("duke/enemygib")
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)

      XDeath:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        goto "Super::XDeath"

      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        BEGR F 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        BEGR F 16 A_Pain
        Goto See

      Death.ZorchPistol:
      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        BEGR A 0 A_Stop
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        BEGR A 3 A_SpawnItem("ZorchEffectSmall")
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        BEGR A 3 A_SetTranslucent(0.9,0)
        BEGR A 0 A_Scream
        BEGR A 3 A_SetTranslucent(0.8,0)
        BEGR A 0 A_NoBlocking
        BEGR A 3 A_SetTranslucent(0.7,0)
        BEGR A 3 A_SetTranslucent(0.6,0)
        BEGR A 3 A_SetTranslucent(0.5,0)
        BEGR A 3 A_SetTranslucent(0.4,0)
        BEGR A 3 A_SetTranslucent(0.3,0)
        BEGR A 3 A_SetTranslucent(0.2,0)
        BEGR A 3 A_SetTranslucent(0.1,0)
        TNT1 A 1 A_SetTranslucent(1,0)
        TNT1 A -1
        stop

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        //TNT1 A 0 A_NoBlocking
        //TNT1 A 0 A_SpawnItem("NaziFrozen")
        BEGR A 0 A_GenericFreezeDeath
        BEGR A 70 A_UnsetSolid
        BEGR A 0 A_IceGuyDie
        BEGR A 1 A_IceGuyDie
        TNT1 A -1
        stop
    }
}

ACTOR Beggar1B : BeggarB replaces Beggar1		// Our bestest friend MacGuffin.
{
	ConversationID 38, 37, 38
}

ACTOR Beggar2B : BeggarB replaces Beggar2		// Some random bum.
{
	ConversationID 39, 38, 39
}

ACTOR Beggar3B : BeggarB replaces Beggar3
{
	ConversationID 40, 39, 40
}

ACTOR Beggar4B : BeggarB replaces Beggar4
{
	ConversationID 41, 40, 41
}

ACTOR Beggar5B : BeggarB replaces Beggar5
{
	ConversationID 42, 41, 42
}
