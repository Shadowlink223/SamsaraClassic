actor " Shotgun " : Shotgun
{
    Weapon.AmmoType "Shell"
    Decal BulletChip
    Weapon.SlotNumber 2
    +THRUGHOST
    +INVENTORY.UNDROPPABLE
    AttackSound ""
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT2"
	Inventory.RestrictedTo "DoomguyPlayer"
    States
    {
      Ready:
        DSHT A 1 A_WeaponReady
        Loop

      Deselect:
        DSHT A 1 A_Lower
        Loop

      Select:
        DSHT A 1 A_Raise
        Loop

      Fire:
        DSHT A 3
        DSHT A 0 A_PlayWeaponSound("doomguy/shotgf")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        TNT1 A 0 A_FireBullets (5.6, 0, 7, 5, "DoomBulletPuff")
        goto FireAnim

      CoopFire:
        TNT1 AAAAAA 0 A_FireCustomMissile("DoomBullet", random(-560, 560)/100.00, 0, 0, 1, 0)
        TNT1 A      0 A_FireCustomMissile("DoomBullet", random(-560, 560)/100.00, 1, 0, 1, 0)
        goto FireAnim

      FireAnim:
        DSHT A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
        TNT1 A 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "RageAnim")
        TNT1 A 0 A_ZoomFactor(0.995, ZOOM_INSTANT)
        DSHT A 2 bright
        DSHT A 5 bright A_ZoomFactor(1, ZOOM_INSTANT)
        DSHT BCDEE 1
        DSHT F 3
        DSHT G 3
        DSHT H 4
        DSHT G 4
        DSHT F 3
        DSHT ED 2
        DSHT C 1
        DSHT B 3 A_Refire
        DSHT A 4 A_ReFire
        Goto Ready

      RageAnim:
        DSHT A 2 bright
        DSHT A 6 bright A_ZoomFactor(1, ZOOM_INSTANT)
        DSHT CE 2
        DSHT FG 4
        DSHT H 6
        DSHT GF 4
        DSHT EDC 2
        DSHT B 2 A_Refire
        DSHT A 4 A_Refire
        goto Ready

      VanillaFire:
        DSHT A 7 bright
        DSHT IJ 5
        DSHT K 4
        DSHT JI 5
        DSHT A 3
        DSHT A 7 A_ReFire
        Goto Ready

      Flash:
        DSHF A 3 bright A_Light1
        DSHF B 4 bright A_Light2
        Goto LightDone

      Spawn:
        WSHT A -1
        Stop
    }
}
