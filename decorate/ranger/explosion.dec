actor QuakeExplosionCounter: Counter {}

actor QuakeExplosion
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD
    
    RenderStyle Add
    
    States
    {
      Spawn:
        QEXP A 0 bright
        QEXP A 0 ACS_ExecuteWithResult(SAMSARA_DECORATE, 8)
        goto SpawnLoop

      SpawnLoop:
        QEXP A 0 A_JumpIfInventory("QuakeExplosionCounter", 1, 1)
        goto Death
        QEXP A 0 A_CustomMissile("QuakeExplosionParticle",  0, 0, random(-180, 180), 2, random(-180, 180))
        QEXP A 0 A_CustomMissile("QuakeExplosionParticle2", 0, 0, random(-180, 180), 2, random(-180, 180))
        QEXP A 0 A_CustomMissile("QuakeExplosionParticle3", 0, 0, random(-180, 180), 2, random(-180, 180))
        QEXP A 0 ACS_ExecuteWithResult(SAMSARA_DECORATE, 10, 1)
        loop
        
      Death:
        QEXP A 4 bright A_PlaySound("quakeweps/explode")
        QEXP B 4 bright
        QEXP CD 3 bright
        QEXP EF 2 bright
        stop
    }
}

actor QuakeTinyExplosion: QuakeExplosion
{
    
    States
    {
      Spawn:
        QEXP A 0 bright
        QEXP A 0 ACS_ExecuteWithResult(SAMSARA_DECORATE, 8, 1, 2)
        goto SpawnLoop

      SpawnLoop:
        QEXP A 0 A_JumpIfInventory("QuakeExplosionCounter", 1, 1)
        goto Death
        QEXP A 0 A_CustomMissile("QuakeTinyParticle",  0, 0, random(-180, 180), 2, random(-180, 180))
        QEXP A 0 A_CustomMissile("QuakeTinyParticle2", 0, 0, random(-180, 180), 2, random(-180, 180))
        QEXP A 0 A_CustomMissile("QuakeTinyParticle3", 0, 0, random(-180, 180), 2, random(-180, 180))
        QEXP A 0 ACS_ExecuteWithResult(SAMSARA_DECORATE, 10, 1)
        loop
        
      Death:
        QEXP A 4 bright A_PlaySound("quakeweps/explode", CHAN_AUTO, 0.5)
        QEXP B 4 bright
        QEXP CD 3 bright
        QEXP EF 2 bright
        stop
    }
}

actor QuakeExplosionParticle
{
    Radius 0
    Height 0
    
    Speed 4
    
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD
    
    Scale 0.225
    Renderstyle Add
    Alpha 0.7
    
    States
    {
      Spawn:
        QPRT D 0 bright
        QPRT D 0 ACS_ExecuteAlways(586, 0, 0, random(90, 110), 0)
        QPRT DE 0 A_Jump(128, "SpawnLoop")
        goto SpawnLoop
        
      SpawnLoop:
        "####" # 2 bright A_FadeOut(0.05)
        loop
    }
}

actor QuakeExplosionParticle2: QuakeExplosionParticle
{
    Speed 12
    Alpha 1
    
    States
    {
      SpawnLoop:
        "####" # 2 bright A_FadeOut(0.1)
        loop
    }
}

actor QuakeExplosionParticle3: QuakeExplosionParticle
{
    Speed 2
    Alpha 1
    
    States
    {
      SpawnLoop:
        "####" # 2 bright A_FadeOut(0.04)
        loop
    }
}

actor QuakeExplosionParticle4: QuakeExplosionParticle
{
    Speed 20
    Alpha 1
    
    States
    {
      SpawnLoop:
        "####" # 2 bright A_FadeOut(0.05)
        loop
    }
}

actor QuakeTinyParticle:  QuakeExplosionParticle  { Speed 2  Scale 0.15 }
actor QuakeTinyParticle2: QuakeExplosionParticle2 { Speed 6  Scale 0.15 }
actor QuakeTinyParticle3: QuakeExplosionParticle3 { Speed 1  Scale 0.15 }
